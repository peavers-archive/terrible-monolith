plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.diffplug.gradle.spotless' version '4.3.0'
    id "com.github.spotbugs" version "4.3.0"
    id 'com.google.cloud.tools.jib' version '2.3.0'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    // Cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.hazelcast:hazelcast-spring'

    // Apache
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation 'commons-io:commons-io:2.7'

    // Google
    implementation 'com.google.guava:guava:29.0-jre'

    // Search
    implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.7.1'
    implementation group: 'org.elasticsearch.client', name: 'elasticsearch-rest-client', version: '7.7.1'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.7.1'

    // Jackson addons
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Development
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

jib {
    from {
        image = 'peavers/terrible-docker-base:latest'
    }
    to {
        image = 'peavers/terrible-monolith'
        auth {
            username = System.getenv("DOCKER_USER")
            password = System.getenv("DOCKER_PASS")
        }
        tags = ["latest", "${project.name}-${project.version}".toString()]
    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
        jvmFlags = ['-Dspring.profiles.active=production', '-XX:+UseContainerSupport', '-Dserver.port=8080', '-Dfile.encoding=UTF-8']
        ports = ['8080']
        labels = [maintainer: 'Chris Turner<peavers@gmail.com>']
    }
}

spotless {
    java {
        licenseHeader '/* Licensed under Apache-2.0 */'
        importOrder 'java', 'javax', 'org', 'com', ''
        removeUnusedImports()
        googleJavaFormat()
    }
}

build.dependsOn spotlessJavaApply

