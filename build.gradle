plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.diffplug.gradle.spotless' version '4.0.1'
    id "com.github.spotbugs" version "4.2.3"
    id 'java'
}

jar {
    enabled = false
}

allprojects {
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'java'

    group = 'io.terrible'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        // Apache
        implementation 'org.apache.commons:commons-lang3:3.10'
        implementation 'commons-io:commons-io:2.6'

        // Google
        implementation 'com.google.guava:guava:29.0-jre'

        // Development
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Tests
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            licenseHeader '/* Licensed under Apache-2.0 */'
            importOrder 'java', 'javax', 'org', 'com', ''
            removeUnusedImports()
            googleJavaFormat()
        }
    }

    build.dependsOn spotlessJavaApply
}
